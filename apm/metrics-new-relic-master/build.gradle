import java.time.Instant

buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.3.1'
  }
}

subprojects { project ->
  apply plugin: 'java'
  apply plugin: 'com.jfrog.bintray'
  apply plugin: 'maven-publish'

  group = 'com.palominolabs.metrics'
  version = '1.1.2-SNAPSHOT'
  ext.isReleaseVersion = !version.endsWith('SNAPSHOT')
  ext.deps = [jackson: '2.6.1',
              junit  : '4.12']

  sourceCompatibility = JavaVersion.VERSION_1_6

  repositories {
    jcenter()
  }

  task sourceJar(type: Jar, dependsOn: classes) {
    from sourceSets.main.allJava
  }

  task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
  }

  // build a pom
  publishing {
    publications {
      bintray(MavenPublication) {
        from components.java
        groupId project.group
        artifactId project.name
        version project.version

        artifact sourceJar {
          classifier 'sources'
        }

        artifact javadocJar {
          classifier 'javadoc'
        }
      }
    }
  }

  project.bintray {
    user = rootProject.hasProperty('bintrayUser') ? rootProject.property('bintrayUser') : 'FIXME'
    key = rootProject.hasProperty('bintrayApiKey') ? rootProject.property('bintrayApiKey') : 'FIXME'
    publications = ['bintray']

    pkg {
      repo = 'maven'
      licenses = ['WTFPL']
      vcsUrl = 'https://github.com/palominolabs/metrics-new-relic'
      name = 'com.palominolabs.metrics:metrics-new-relic'

      version {
        name = project.version
        released = Date.from(Instant.now())
        if (project.isReleaseVersion) {
          vcsTag = 'v' + project.version
        }
      }
    }
  }
}


task wrapper(type: Wrapper) {
  gradleVersion = '2.6'
}
